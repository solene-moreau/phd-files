/*************************************
LAK (WITH PAIRS)

[1] T. van Deursen and S. Radomirović,
‘Attacks on RFID Protocols’,
Cryptology ePrint Archive, vol. 2008, no. 310, pp. 1–56, Aug. 2009.

DESCRIPTION OF THE PROTOCOL
[init.]   kT <-- h(k), insert k,h(k) in DB
[session] R --> T : nR
          T --> R : <nT,h(<xnR,nT,kT>)>
          R --> T : h(<h(<nR,xnT,kR>),kR,nR>)     if there exists (kR0,kR) in DB
                                                    such that snd(x) = h(<nR,fst(x),kR>)
                                                  (kR0,kR) <-- (kR,h(kR))
          R --> T : h(<h(<nR,xnT,kr0>),kr0,nR>)   if there exists (kR0,kR) in DB
                                                    such that snd(x) = h(<nR,fst(x),kR0>)
                                                  (kR0,kR) <-- (kR0,kR)
          T --> R : ok                            if x = h(<h(<xnR,nT,kT>),kT,xnR>)
                                                  kT <-- h(kT)
          T --> R : error                         otherwise

PROOFS
- sanity checks
  * readerComplete            => true (automatic mode)
  * tagComplete               => true (automatic mode)
- helpful lemmas
  * ifHashKnownThenKeyKnown   => true (automatic mode)
  * initStateDB               => true (automatic mode)
  * initStateTag              => true (automatic mode)
  * lookupImpliesInsert       => true (automatic mode)
  * secrecyDB1                => true (automatic mode)
  * secrecyDB2                => true (automatic mode)
  * secrecyIdentityTag        => true (automatic mode)
- well-authentication
  * WA_Tag                    => true (automatic mode)
  * WA_Reader                 => true (automatic mode)
- no desynchronisation
  * ND_Tag                    => true (automatic mode)
  * ND_Reader                 => false (automatic mode)
*************************************/

theory LAK_WA_ND
begin

functions:
  h/3, hkey/1,
  OK/0, ERROR/0

/*************************************
PROTOCOL
*************************************/

rule InitReader:
  [ Fr(~sidR) ]
  --[ NewReaderSession(~sidR) ]->
  [ Reader(~sidR) ]

rule InitTagId:
  [ Fr(~k) ]
  --[ NewTagState(~k,hkey(~k)), InsertDB(~k,hkey(~k)), Fresh(~k), StateDB(hkey(~k)) ]->
  [ DB(~k,hkey(~k)), !TagSession(~k,hkey(~k)) ]

rule InitTagSession:
  [ !TagSession(idT,kT), Fr(~sidT) ]
  --[ NewTagSession(~sidT,idT,kT), StateT(kT) ]->
  [ Tag(~sidT,idT,kT) ]

/* READER */

rule R_out1:
  let m1 = ~nR in
  [ Reader(~sidR),  Fr(~nR)]
  --[ OutR1(~sidR,m1) ]->
  [ Reader1(~sidR,m1), Out(m1) ]

rule R_in1:
  [ Reader1(~sidR,m1), In(x2) ]
  --[ InR1(~sidR,m1,x2) ]->
  [ Reader2(~sidR,m1,x2) ]

rule R_test1: // (kR0,kR) <-- (kR,h(kR))
  let x2 = <xnT,h(m1,xnT,kR)> in
  [ DB(kR0,kR), Reader2(~sidR,m1,x2) ]
  --[ InsertDB(kR,hkey(kR)), RemoveDB(kR0,kR), StateDB(hkey(kR)), TestR1(~sidR) ]->
  [ DB(kR,hkey(kR)), Reader3(~sidR,m1,x2,xnT,kR) ]

rule R_test1_resync: // (kR0,kR) <-- (kR0,kR)
  let x2 = <xnT,h(m1,xnT,kR0)> in
  [ DB(kR0,kR), Reader2(~sidR,m1,x2) ]
  --[ NoUpdateDB(kR0,kR), StateDB(hkey(kR)), TestR(~sidR) ]->
  [ DB(kR0,kR), Reader3(~sidR,m1,x2,xnT,kR0) ]

rule R_out2:
  let m3 = h(h(m1,xnT,xkT),xkT,m1) in
  [ Reader3(~sidR,m1,x2,xnT,xkT) ]
  --[ OutR2(~sidR,m1,x2,m3), CompleteR(~sidR) ]->
  [ Out(m3) ]

/* TAG */

rule T_in1:
  [ Tag(~sidT,idT,kT), In(x1) ]
  --[ PlayT(~sidT,idT,kT), InT1(~sidT,kT,x1) ]->
  [ Tag1(~sidT,idT,kT,x1) ]

rule T_out1:
  let m2 = <~nT,h(x1,~nT,kT)> in
  [ Tag1(~sidT,idT,kT,x1), Fr(~nT) ]
  --[ PlayT(~sidT,idT,kT), OutT1(~sidT,kT,~nT,x1,m2) ]->
  [ Tag2(~sidT,idT,kT,~nT,x1,m2), Out(m2) ]

rule T_in2:
  [ Tag2(~sidT,idT,kT,~nT,x1,m2), In(x3) ]
  --[ PlayT(~sidT,idT,kT), InT2(~sidT,kT,~nT,x1,m2,x3) ]->
  [ Tag3(~sidT,idT,kT,~nT,x1,m2,x3) ]

rule T_test_then:
  let x3 = h(h(x1,~nT,kT),kT,x1) in
  [ Tag3(~sidT,idT,kT,~nT,x1,m2,x3) ]
  --[ PlayT(~sidT,idT,kT), TestT(~sidT), CompleteT(~sidT), NewTagState(idT,hkey(kT)), StateT(hkey(kT)) ]->
  [ !TagSession(idT,hkey(kT)), Out(OK) ]

rule T_test_else:
  [ Tag3(~sidT,idT,kT,~nT,x1,m2,x3) ]
  --[ PlayT(~sidT,idT,kT), NotEqual(x3,h(h(x1,~nT,kT),kT,x1)) ]->
  [ Out(ERROR) ]

/*************************************
RESTRICTIONS
*************************************/

// no interleaving sessions for a given tag
restriction seqSessionTag:
  "not (Ex sidT1 sidT2 idT kT1 kT2 kT3 #i1 #i2 #i3.
    PlayT(sidT1,idT,kT1)@i1 &
    PlayT(sidT2,idT,kT2)@i2 &
    PlayT(sidT1,idT,kT3)@i3 &
    i1<i2 & i2<i3 &
    not(sidT1=sidT2)
  )"

// once the tag has updated its state, no session with a previous state
restriction updateTag:
  "not (Ex sidT idT kT #i1 #i2.
    NewTagState(idT,hkey(kT))@i1 &
    PlayT(sidT,idT,kT)@i2 &
    i1 < i2
  )"

/*************************************
SANITY CHECKS
*************************************/

lemma readerComplete:
  exists-trace
    "Ex sidR #i. CompleteR(sidR)@i"

lemma tagComplete:
  exists-trace
    "Ex sidT #i. CompleteT(sidT)@i"

/*************************************
NO DESYNCHRONISATION
*************************************/

lemma ND_Tag:
  "All sidT kT nT sidR m1 m2 m3 #i1 #i2 #i3 #i4 #i5 #i6.
    ( InT2(sidT,kT,nT,m1,m2,m3)@i6 &
      OutR2(sidR,m1,m2,m3)@i5 &
      InR1(sidR,m1,m2)@i4 &
      OutT1(sidT,kT,nT,m1,m2)@i3 &
      InT1(sidT,kT,m1)@i2 &
      OutR1(sidR,m1)@i1 &
      i1<i2 & i2<i3 & i3<i4 & i4<i5 & i5<i6
    ) ==> (
      m3 = h(h(m1,nT,kT),kT,m1)
    )
  "

lemma ND_Reader:
  "All sidT kT nT sidR m1 m2 #i1 #i2 #i3 #i4.
    ( InR1(sidR,m1,m2)@i4 &
      OutT1(sidT,kT,nT,m1,m2)@i3 &
      InT1(sidT,kT,m1)@i2 &
      OutR1(sidR,m1)@i1 &
      i1<i2 & i2<i3 & i3<i4
    ) ==> (
      (
        Ex #i5. TestR(sidR)@i5 & i4<i5
      ) | (
        Ex xnT kR0 kR #i0.
          m2 = <xnT,h(m1,xnT,kR)> &
          InsertDB(kR0,kR)@i0 & i0<i4 &
          not(Ex #i6. RemoveDB(kR0,kR)@i6 & i0<i6)
      ) | (
        Ex xnT kR0 kR #i0.
          m2 = <xnT,h(m1,xnT,kR0)> &
          InsertDB(kR0,kR)@i0 & i0<i4 &
          not(Ex #i6. RemoveDB(kR0,kR)@i6 & i0<i6)
        )
    )
  "

/*************************************
HELPFUL LEMMAS
*************************************/

lemma ifHashKnownThenKeyKnown [reuse, use_induction]:
 "All k #i. KU(hkey(k))@i
   ==> Ex #j. (KU(k)@j & j<i)"

lemma initStateDB [reuse, use_induction]:
  "All k #i. StateDB(hkey(k))@i
    ==> Fresh(k)@i | ( Ex k0 #j. Fresh(k0)@j & j<i )"

lemma initStateTag [reuse, use_induction]:
  "All k #i. StateT(hkey(k))@i
    ==> ( Ex k0 #j. Fresh(k0)@j & j<i )"

lemma lookupImpliesInsert [reuse, use_induction]:
  "All kR0 kR #i. NoUpdateDB(kR0,kR)@i
    ==> Ex #j. (InsertDB(kR0,kR)@j & j<i)"

lemma secrecyDB1 [reuse, use_induction] :
  "not(Ex kR0 kR #i1 #i2. (InsertDB(kR0,kR)@i1 & KU(kR0)@i2))"

lemma secrecyDB2 [reuse, use_induction] :
  "not(Ex kR0 kR #i1 #i2. (InsertDB(kR0,kR)@i1 & KU(kR)@i2))"

lemma secrecyIdentityTag [reuse, use_induction] :
  "not(Ex sidT idT kT #i1 #i2. (NewTagSession(sidT,idT,kT)@i1 & KU(kT)@i2))"

/*************************************
WELL-AUTHENTICATION
*************************************/

lemma WA_Tag [use_induction]:
  "All sidT #i7.
    TestT(sidT)@i7 ==>
      ( Ex kT nT sidR m1 m2 m3 #i1 #i2 #i3 #i4 #i5 #i6.
        (
          InT2(sidT,kT,nT,m1,m2,m3)@i6 &
          OutR2(sidR,m1,m2,m3)@i5 &
          InR1(sidR,m1,m2)@i4 &
          OutT1(sidT,kT,nT,m1,m2)@i3 &
          InT1(sidT,kT,m1)@i2 &
          OutR1(sidR,m1)@i1 &
          i1<i2 & i2<i3 & i3<i4 & i4<i5 & i5<i6 & i6<i7
        )
      )
  "

lemma WA_Reader [use_induction]:
  "All sidR #i5.
    TestR(sidR)@i5 ==>
      ( Ex sidT kT nT m1 m2 #i1 #i2 #i3 #i4.
        (
          InR1(sidR,m1,m2)@i4 &
          OutT1(sidT,kT,nT,m1,m2)@i3 &
          InT1(sidT,kT,m1)@i2 &
          OutR1(sidR,m1)@i1 &
          i1<i2 & i2<i3 & i3<i4 & i4<i5
        )
      )
  "

end
