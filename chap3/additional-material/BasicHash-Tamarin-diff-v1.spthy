/*************************************
BASIC HASH

[1] M. Brusó, K. Chatzikokolakis, and J. d Hartog,
‘Formal Verification of Privacy for RFID Systems’,
in 2010 23rd IEEE Computer Security Foundations Symposium, 2010, pp. 75–88.

DESCRIPTION OF THE PROTOCOL
[init.]   kT <-- k, insert k in DB
[session] T --> R : < nT, h(kT,nT) >
          R --> T : OK                if there exists kR in DB
                                         such that snd(x) = h(kR,fst(x))

PROOFS
- sanity checks
  * readerComplete            => true (automatic mode)
  * tagComplete               => true (automatic mode)
- unlinkability (M_pi = S_pi)
  * Observational_equivalence => true (automatic mode)

On this file, Tamarin concludes that observational equivalence holds but we
encoded the equivalence M_pi = S_pi using a restriction that applies only on
the right-hand side, which is not safe as reported in the following bug issue:
https://github.com/tamarin-prover/tamarin-prover/issues/324
*************************************/

theory BasicHash_diff_v1
begin

functions: h/1, OK/0

/*************************************
PROTOCOL
*************************************/

rule InitReader:
  [ Fr(~sidR) ]
  --[ NewReaderSession(~sidR) ]->
  [ Reader(~sidR) ]

rule InitTagId:
  [ Fr(~k) ]
  --[ NewId(~k), InsertDB(~k) ]->
  [ !DB(~k), !TagSession(~k) ]

rule InitTagSession:
  [ !TagSession(~k), Fr(~sidT) ]
  --[ NewTagSession(~sidT, ~k) ]->
  [ Tag(~sidT, ~k) ]

/* READER */

rule R_in:
  [ Reader(~sidR),  In(x) ]
  --[ InR(~sidR,x) ]->
  [ Reader1(~sidR,x) ]

rule R_test:
  let x = <xnT,h(<kR,xnT>)> in
  [ !DB(kR), Reader1(~sidR,x) ]
  --[ TestR(~sidR), CompleteR(~sidR) ]->
  [ Out(OK) ]

/* TAG */

rule T_out:
  let m = <~nT,h(<kT,~nT>)> in
  [ Tag(~sidT,kT), Fr(~nT) ]
  --[ PlayT(~sidT,kT), OutT(~sidT,kT,~nT,m), CompleteT(~sidT) ]->
  [ Out(m) ]


/*************************************
RESTRICTIONS
*************************************/

// LEFT (multiple sessions)
// No restrictions are necessary to model the sequentiality of a tag
// because interleaving sessions are not possible for the tag
// and the state kT is not updated.

// RIGHT (single session)
// A tag with identity kT only plays once.
restriction singleSessionTag [right]:
  "not (Ex sidT1 sidT2 kT #i1 #i2.
    PlayT(sidT1,kT) @ i1 &
    PlayT(sidT2,kT) @ i2 &
    not(#i1 = #i2)
  )"

/*************************************
SANITY CHECKS
*************************************/

lemma readerComplete:
  exists-trace
  "Ex sidR #i. CompleteR(sidR)@i"

lemma tagComplete:
  exists-trace
  "Ex sidT #i. CompleteT(sidT)@i"

end
